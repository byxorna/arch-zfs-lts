# Maintainer: Gabe Conradi <gummybearx at gmail dot com>

pkgname="zfs-utils-lts"
#
# This script doesn't use bash variables for the version number because AUR
# doesn't properly parse bash variables. We use a custom build script to
# automatically change the appropriate variables before building in a clean
# chroot environment (using systemd-ndspawn).
#
# The build script can be found at
# https://github.com/demizer/archzfs/blob/master/build.sh
#
zfsver=0.6.2
pkgver="${zfsver}_3.10.36"
pkgrel=1
pkgdesc="Kernel module support files for the Zettabyte File System."
depends=("spl-lts")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-${zfsver}.tar.gz"
        zfs-utils.bash-completion
        zfs-utils.initcpio.install
        zfs-utils.initcpio.hook
        zfs-utils.service)
groups=("archzfs")
md5sums=('0b183b0abdd5be287046ad9ce4f899fd'
         '3e1c4a29c4f7d590e6a3041f2c61d6ff'
         'ebea2b1e593a9a19efa0f7ae7451bbc7'
         'b567a95a34759f196bf4be8061a895e5'
         '1f16ea8592509edb8efa4d7da25f8182')
license=("CDDL")

build() {
  cd "$srcdir/zfs-${zfsver}"
  ./autogen.sh
  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --sbindir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/lib/udev \
                --libexecdir=/usr/lib/zfs-$zfsver \
                --with-config=user \
                --with-linux=/usr/lib/modules/$LINUX_VERSION_X32-lts/build
  else
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --sbindir=/usr/bin \
                --libdir=/usr/lib \
                --datadir=/usr/share \
                --includedir=/usr/include \
                --with-udevdir=/lib/udev \
                --libexecdir=/usr/lib/zfs-$zfsver \
                --with-config=user \
                --with-linux=/usr/lib/modules/$LINUX_VERSION_X64-lts/build
  fi
  make
}

package() {
  cd "$srcdir/zfs-${zfsver}"
  make DESTDIR="$pkgdir" install

  # Remove uneeded files
  rm -r "$pkgdir"/etc/init.d
  rm -r "$pkgdir"/usr/lib/dracut

  # move module tree /lib -> /usr/lib
  cp -r "$pkgdir"/{lib,usr}
  rm -r "$pkgdir"/lib

  # Fixup path
  mv "$pkgdir"/sbin/mount.zfs "$pkgdir"/usr/bin/
  rm -r "$pkgdir"/sbin

  install -D -m644 "$srcdir"/zfs-utils.initcpio.hook "$pkgdir"/usr/lib/initcpio/hooks/zfs
  install -D -m644 "$srcdir"/zfs-utils.initcpio.install "$pkgdir"/usr/lib/initcpio/install/zfs
  install -D -m644 "$srcdir"/zfs-utils.service "$pkgdir"/usr/lib/systemd/system/zfs.service
  install -D -m644 "$srcdir"/zfs-utils.bash-completion "$pkgdir"/usr/share/bash-completion/completions/zfs
}
